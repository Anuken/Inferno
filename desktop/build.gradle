apply plugin: "java"

sourceCompatibility = 1.8
sourceSets.main.java.srcDirs = [ "src/" ]

project.ext.mainClassName = "inferno.desktop.DesktopLauncher"
project.ext.assetsDir = new File("../core/assets");

task run(dependsOn: classes, type: JavaExec) {
    main = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.assetsDir
    ignoreExitValue = true

    if(System.getProperty("os.name").toLowerCase().contains("mac")){
        jvmArgs("-XstartOnFirstThread", "-Djava.awt.headless=true")
    }
}

task dist(type: Jar) {
    from files(sourceSets.main.output.classesDirs)
    from files(sourceSets.main.output.resourcesDir)
    from {configurations.compile.collect {zipTree(it)}}
    from files(project.assetsDir);

    if(project.hasProperty("target")){
        def target = project.properties["target"]
        if(target == "windows") exclude('**.so', "**.dylib")
        if(target == "mac") exclude('**.so', "**.dll")
        if(target == "linux") exclude('**.dll', "**.dylib")
        archivesBaseName = appName + "-" + target
    }else{
        archivesBaseName = appName + "-all"
    }

    manifest {
        attributes 'Main-Class': project.mainClassName
    }
}

dist.dependsOn classes
